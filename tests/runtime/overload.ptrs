import assert, assertEq from "../common.ptrs";

var op = "none";
var val = 0;
struct Test
{
	constructor(a, b, c)
	{
		op = "ctor";
		val = new var[3] [a, b, c];
	}


	operator cast<int>this
	{
		op = "toint";
		return 3;
	}
	operator cast<float>this
	{
		op = "tofloat";
		return 3.14;
	}
	operator cast<string>this
	{
		op = "tostr";
		return "4";
	}

	operator this(a, b)
	{
		op = "call";
		val = new var[2] [a, b];
	}

	operator this[key]
	{
		op = ".get";
		val = key;
		return 6;
	}
	operator this[key] = rval
	{
		op = ".set";
		val = new var[2] [key, rval];
		return 7;
	}
	operator this[key](a, b)
	{
		op = ".call";
		val = new var[3] [key, a, b];
		return 8;
	}

	operator key in this
	{
		op = "in";
		val = key;
		return 9;
	}

	operator foreach(fields, saveArea) in this
	{
		op = "each";
		assertEq(1, sizeof fields);
		assertEq(1, sizeof saveArea);

		var i = *saveArea;
		if(typeof i != type<int>)
		{
			assertEq(undefined, i);
			i = 0;
		}

		if(i >= 4)
		{
			throw "this shouldn't happen";
			return false;
		}

		if(i == 0)
			val = false;
		else if(i == 1)
			val = "hi";
		else if(i == 2)
			val = 31.12;
		else if(i == 3)
			val = 42;

		fields[0] = val;
		*saveArea = i + 1;
		return true;
	}
};

var test = new Test(42, "kek", 3.14);
assertEq("ctor", op);
assertEq(42, val[0]);
assertEq("kek", val[1]);
assertEq(3.14, val[2]);
delete val;

assertEq(3, cast<int>test);
assertEq("toint", op);

assertEq(3.14, cast<float>test);
assertEq("tofloat", op);

assertEq("4", cast<string>test);
assertEq("tostr", op);

assertEq(6, test.foo);
assertEq(".get", op);
assertEq("foo", val);

assertEq(42f, test.bar = 42f);
assertEq(".set", op);
assertEq("bar", val[0]);
assertEq(42f, val[1]);
delete val;

//TODO implement this[key]() overloads
/*assertEq(8, test.tar("devil", 666));
assertEq(".call", op);
assertEq("tar", val[0]);
assertEq("devil", val[1]);
assertEq(666, val[2]);
delete val;*/

assertEq(true, "xar" in test);
assertEq("in", op);
assertEq("xar", val);

foreach(val2 in test)
{
	assertEq(val, val2);
	if(val == 42)
		break;
}
assertEq("each", op);
